= Deps and CLI Guide
Alex Miller
2017-09-08
:type: guides
:toc: macro
:icons: font

ifdef::env-github,env-browser[:outfilesuffix: .adoc]

toc::[]

Clojure provides command line tools for:

* Running an interactive REPL (Read-Eval-Print Loop)
* Running Clojure programs
* Evaluating Clojure expressions

In all the above scenarios you might want to use other Clojure and Java libraries (dependencies or 'deps'). These may be libraries you are writing locally, projects in git (e.g. on github) or, commonly, libraries available in the Maven ecosystem and hosted by central repositories like Maven Central or Clojars.

In all cases, using a library involves: +
a) specifying which library you want to use, providing its name and other aspects like version +
b) getting it (once) from the git or maven repositories to your local machine +
c) making it available on the JVM classpath so Clojure can find it while your REPL or program is running +

Clojure tools specify a syntax and file (deps.edn) for (a), given which they'll handle (b) and (c) automatically.

See <<getting_started#,Getting Started>> for details on how to install the tools. Here we will demonstrate how to get started. See <<xref/../../reference/deps_and_cli#,Deps and CLI>> for a complete reference.

== Running a REPL and using libraries

After you download and install the tools, you can start a REPL by running the `clj` tool:

[source,shell]
----
$ clj
Clojure 1.9.0
user=>
----

Once in the REPL you can type Clojure expressions and press enter to evaluate them.

There are many Clojure and Java libraries available that provide access to practically any functionality you might need. These may be libraries you are writing locally, projects in git (e.g. on github) or, commonly, libraries available in the Maven ecosystem and hosted by central repositories like Maven central or Clojars. 

For example, consider the commonly used Clojure library https://github.com/clj-time/clj-time[clj-time] for working with dates and times. If you tried to use it at the REPL, it would fail:

[source,clojure]
----
user=> (require '[clj-time.core :as t])
FileNotFoundException Could not locate clj_time/core__init.class or clj_time/core.clj on classpath.
Please check that namespaces with dashes use underscores in the Clojure file name.
----

The error message indicates that the `clj-time.core` namespace isn't on the JVM's classpath. The classpath specifies all of the places the JVM will look to load Clojure namespaces (or Java classes).

Exit the REPL by typing Ctrl-D at the prompt to return to the terminal. Create a file in the current directory named `deps.edn`. This file is in the https://github.com/edn-format/edn[edn] data format, a subset of Clojure syntax. It contains a map with the well-known key `:deps` which is a map from library to coordinate:

[source,clojure]
----
{:deps
 {clj-time {:mvn/version "0.14.2"}}}
----

Restart the REPL with the `clj` tool and try again:

[source,clojure]
----
$ clj
Clojure 1.9.0
user=> (require '[clj-time.core :as t])
nil
user=> (str (t/now))
"2017-12-06T19:36:56.159Z"
----

You can use the same process to add other libraries to your `deps.edn` file and explore Clojure or Java libraries.

== Writing a program

Soon you will want to build and save your own code that makes use of these libraries. Consider the following program that uses clj-time:

[source,clojure]
----
(ns hello.world
  (:require [clj-time.core :as t]
            [clj-time.format :as f]))

(defn time-str
  "Returns a string representation of a datetime in the local time zone."
  [dt]
  (f/unparse
    (f/with-zone (f/formatter "hh:mm aa") (t/default-time-zone))
    dt))

(defn -main []
  (println "Hello world, the time is" (time-str (t/now))))
----

By default, the `clj` tool will look for source files in the `src` directory, so Clojure will look for a file with this namespace at `src/hello/world.clj`.

This program has a static entry point named `-main` that is suitable for external invocation. The `clj` tool can act as a Clojure program launcher by using the `-m` option, which specifies the namespace to run:

[source,shell]
----
$ clj -m hello.world
Hello world, the time is 02:04 PM
----

== Using local libraries

After a while you might decide to break part of this application out as a library. The `clj` tool uses local coordinates to support projects that exist only on your local disk. Let's extract the clj-time parts of this application out into a library called hello/time.

In a new directory, use the deps.edn file you already have, and create a file `src/hello/time.clj`:

[source,clojure]
----
(ns hello.time
  (:require [clj-time.core :as t]
            [clj-time.format :as f]))

(defn now
  "Returns the current datetime"
  []
  (t/now))

(defn time-str
  "Returns a string representation of a datetime in the local time zone."
  [dt]
  (f/unparse
    (f/with-zone (f/formatter "hh:mm aa") (t/default-time-zone))
    dt))
----

Then the original application is reduced to:

[source,clojure]
----
(ns hello.world
  (:require [hello.time :as ht]))

(defn -main []
  (println "Hello world, the time is" (ht/time-str (ht/now))))
----

To create the dependency on the other local library, create a new `deps.edn` and use a local coordinate to refer to the root directory of the hello/time library.:

[source,clojure]
----
{:deps
 {hello/time {:local/root "../time"}}}
----



You can then test everything by running the application:

[source,shell]
----
clj -m hello.world
Hello world, the time is 02:07 PM
----

== Other examples

As your program gets more involved you might need to create variations on the standard classpath. The Clojure tools supports classpath modifications using aliases, which are parts of the deps file that are only used when the corresponding alias is supplied. Some of the things you can do are:

* <<deps_and_cli#extra_paths,Include a test source directory>>
* <<deps_and_cli#extra_deps,Add an optional dependency>>
* <<deps_and_cli#override_deps,Override a dependency version>>
* <<deps_and_cli#local_jar,Use a local jar on disk>>

[[extra_paths]]
=== Include a test source directory

Typically, the project classpath includes only the project source, not its test source by default. You can add extra paths as modifications to the primary classpath in the make-classpath step of the classpath construction. To do so, add an alias `:test` that includes the extra relative source path `"test"`:

[source,clojure]
----
{:deps
 {org.clojure/core.async {:mvn/version "0.3.465"}}

 :aliases
 {:test {:extra-paths ["test"]}}}
----

Apply that classpath modification and examine the modified classpath by invoking `clj -C:test -Spath`:

[source,shell]
----
$ clj -C:test -Spath
src:
test:
/Users/me/.m2/repository/org/clojure/clojure/1.9.0/clojure-1.9.0.jar:
/Users/me/.m2/repository/org/clojure/tools.analyzer/0.6.9/tools.analyzer-0.6.9.jar:
... same as before
----

Note that the test dir is now included in the classpath.

[[extra_deps]]
=== Add an optional dependency

Aliases in the `deps.edn` file can also be used to add optional dependencies that affect the classpath:

[source,clojure]
----
{:aliases
 {:bench {:extra-deps {criterium {:mvn/version "0.4.4"}}}}}
----

Here the `:bench` alias is used to add an extra dependency, namely the criterium benchmarking library.

You can add this dependency to your classpath by adding the `:bench` alias to modify the dependency resolution: `clj -R:bench`.

[[override_deps]]
=== Override a dependency

You can use multiple aliases in combination. For example this `deps.edn` file defines two aliases - `:old-async` to force the use of an older core.async version and `:bench` to add an extra dependency:

[source,clojure]
----
{:deps 
 {org.clojure/core.async {:mvn/version "0.3.465"}}

 :aliases
 {:old-async {:override-deps {org.clojure/core.async {:mvn/version "0.3.426"}}}
  :bench {:extra-deps {criterium {:mvn/version "0.4.4"}}}}}
----

Activate both aliases as follows: `clj -R:bench:old-async`.

[[local_jar]]
=== Include a local jar on disk

Occasionally you may need to refer directly to a jar on disk that is not present in a Maven repository, such as a database driver jar.

Specify local jar dependencies with a local coordinate that points directly to a jar file instead of a directory:

[source,clojure]
----
{:deps
 {db/driver {:local/root "/path/to/db/driver.jar"}}}
----
